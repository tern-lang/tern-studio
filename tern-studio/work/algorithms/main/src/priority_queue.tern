import security.SecureRandom;


func generate(count) {
   const random = new SecureRandom();
   
   for(i in 0 to count) {
      yield random.nextInt(1000000);
   }
}

const queue = new PriorityQueue();
const numbers = generate(10000).iterator().gather();


numbers.iterator().forEachRemaining(queue::offer);
queue.iterator().forEachRemaining(this::println);


class PriorityQueue with Iterable {

   let queue = [];

   public offer(n) {
      queue.add(n);
      moveUp(queue.length -1);
   }
   
   public override iterator() {
      return new SortIterator(this);
   }
   
   public poll(){
      let result = queue[0];
      
      if(queue.length > 1) {
         queue[0] = queue.remove(queue.length -1);
         moveDown(0);
      } else {
         queue.clear();
      }
      return result;
   }
   
   private moveUp(pos){
      let parent = pos /2;
      
      if(parent != pos) {
         let value = queue[parent];
         let curr = queue[pos];
         
         if(value > curr) {
            swap(parent, pos);
            
            if(parent > 0) {
               moveUp(parent);
            }
         }
      }
   }
   
   private moveDown(pos) {
      let left = (pos * 2) + 1;
      let right = left + 1;
      let index = null;
      
      if(right < queue.length){
         index = queue[left] < queue[right] ? left : right;
      } else if(left < queue.length){
         index = left;
      } 
      if(index) {
         let value = queue[index];
         let curr = queue[pos];
         
         if(value < curr){
            swap(index, pos);
            moveDown(index);
         }
      }
      
   }
 
   private swap(a, b) {
      let left = queue[a];
      let right = queue[b];
      
      queue[a] = right;
      queue[b] = left;
   }
      
   public size() {
      return queue.length;
   }
   
   public override toString() {
      return queue.toString();
   }
   
   class SortIterator with Iterator {
   
      private let queue;
      
      new(queue) {
         this.queue = queue;
      }
      
      public override next() {
         return queue.poll();
      }
      
      public override hasNext() {
         return queue.size > 0;
      }
   }
}
